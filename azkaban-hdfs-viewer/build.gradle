apply plugin: 'distribution'

ext.libDir = "$projectDir/lib"

configurations {
  distPackage {
    transitive false
  }
  compile.extendsFrom distPackage
}

dependencies {
  compile project(":azkaban-web-server")
  compile project(":azkaban-hadoop-security-plugin")

  compile "org.apache.hive:hive-exec:$hiveVersion"
  distPackage "org.mongodb:mongo-java-driver:2.10.1"
  distPackage fileTree(dir: "$libDir", include: '*.jar')
}

/**
 * Just package the jar.
 * Since, rest of the dependencies are just hadoop and hive. They are not packaged inside the plugin.
 * It is assumed that classpaths of hadoop, hive, pig, etc will be externally fed into the application.
 */
distributions {
  main {
    contents {
      from(jar) {
        into 'lib'
      }
      from(configurations.distPackage) {
        into 'lib'
      }
    }
  }
}
